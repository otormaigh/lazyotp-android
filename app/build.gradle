import ie.otormaigh.lazyotp.plugin.SemVer

plugins {
  id "app.cash.sqldelight" version "2.0.0-alpha05"
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.dagger.hilt.android'

if (file('../enc.properties').exists()) {
  apply from: '../enc.properties'
}

android {
  namespace "ie.otormaigh.lazyotp"
  compileSdkVersion versions.compileSdk

  defaultConfig {
    applicationId "ie.otormaigh.lazyotp"
    minSdkVersion versions.minSdk
    targetSdkVersion versions.targetSdk
    versionCode SemVer.code
    versionName SemVer.name
    archivesBaseName = "lazyotp-$versionName"
    resConfigs "en", "ga", "it", "es", "fr", "de", "pt"
    resourceConfigurations += ["en", "ga", "it", "es", "fr", "de", "pt"]
  }

  sourceSets.main.java.srcDirs += ['src/main/sqldelight']

  buildFeatures {
    buildConfig true
    viewBinding true
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_17.toString()
  }

  signingConfigs {
    debug {
      storeFile file('../signing/debug.keystore')
    }

    if (file('../signing/release.keystore').exists()) {
      release {
        storeFile file('../signing/release.keystore')
        storePassword project.properties['store_password']
        keyAlias project.properties['key_alias']
        keyPassword project.properties['key_password']
      }
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix ".debug"
      signingConfig signingConfigs.debug
    }

    release {
      signingConfig signingConfigs.debug
      postprocessing {
        proguardFiles 'proguard-rules.pro'
        removeUnusedResources = true
        removeUnusedCode = true
        optimizeCode = true
        obfuscate = true
      }
    }
  }

  packagingOptions {
    exclude "META-INF/atomicfu.kotlin_module"
  }
}

sqldelight {
  databases {
    LazyOtpDatabase {
      packageName = "ie.otormaigh.lazyotp"
      schemaOutputDirectory = file("src/main/sqldelight/ie/otormaigh/lazyotp/databases")
      verifyMigrations = true
    }
  }
}

dependencies {
  implementation deps.kotlin.stdlib
  implementation deps.kotlin.coroutine.core
  implementation deps.kotlin.coroutine.android

  implementation deps.google.material
  implementation deps.androidx.appcompat
  implementation deps.androidx.core_ktx
  implementation deps.androidx.constraintlayout
  implementation deps.androidx.prefs
  implementation deps.androidx.viewmodel
  implementation deps.androidx.work.runtime_ktx

  implementation deps.square.retrofit.core
  implementation deps.square.okhttp.core
  implementation deps.square.okhttp.logging
  implementation deps.cashapp.sqldelight.android_driver

  implementation deps.google.hilt.core
  kapt deps.google.hilt.compiler

  implementation deps.timber

  testImplementation deps.google.truth
  testImplementation deps.junit
  testImplementation "io.mockk:mockk:1.13.5"
}

tasks.getByName("preBuild").dependsOn(":app:generateSqlDelightInterface")