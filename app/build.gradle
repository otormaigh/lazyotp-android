apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

if (file('../enc.properties').exists()) {
  apply from: '../enc.properties'
}

android {
  compileSdkVersion 29
  defaultConfig {
    applicationId "ie.otormaigh.lazyotp"
    minSdkVersion 21
    targetSdkVersion 29
    versionCode 3
    versionName "0.1.$versionCode"
    archivesBaseName = "lazyotp-$versionName"

    buildConfigField "String", "SLACK_TOKEN", project.properties["slack_token"]

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
      }
    }
  }

  sourceSets {
    androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8
  }

  signingConfigs {
    if (file('../signing/release.keystore').exists()) {
      release {
        storeFile file('../signing/release.keystore')
        storePassword project.properties['store_password']
        keyAlias project.properties['key_alias']
        keyPassword project.properties['key_password']
      }
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix ".debug"
    }

    release {
      if (file('../signing/release.keystore').exists()) {
        signingConfig signingConfigs.release
      }
      postprocessing {
        proguardFiles 'proguard-rules.pro'
        removeUnusedResources = true
        removeUnusedCode = true
        optimizeCode = true
        obfuscate = true
      }
    }
  }

  packagingOptions {
    exclude "META-INF/atomicfu.kotlin_module"
  }
}

dependencies {
  def coroutines = "1.3.0-RC2"
  def retrofit = "2.6.1"
  def okhttp3 = "4.1.0"
  def room = "2.2.0-alpha02"

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"

  implementation 'com.google.android.material:material:1.1.0-alpha09'
  implementation 'androidx.appcompat:appcompat:1.1.0-rc01'
  implementation 'androidx.core:core-ktx:1.2.0-alpha03'
  implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'
  implementation 'androidx.preference:preference:1.1.0-rc01'
  implementation 'androidx.work:work-runtime-ktx:2.2.0'

  implementation "androidx.room:room-ktx:$room"
  implementation "androidx.room:room-runtime:$room"
  kapt "androidx.room:room-compiler:$room"

  implementation "com.squareup.retrofit2:retrofit:$retrofit"
  implementation "com.squareup.okhttp3:okhttp:$okhttp3"
  implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3"

  implementation 'com.jakewharton.timber:timber:4.7.1'

  testImplementation 'junit:junit:4.13-beta-3'
  testImplementation 'com.squareup.assertj:assertj-android:1.2.0'
}

androidExtensions {
  experimental = true
}